plugins {
    id 'java-library'
    id 'maven-publish'

    // the below build plugin allows to generate BuildConfig files in a JAVA gradle project.
    // BuildConfig files are generated by default in an Android project, however, a non-Android java
    // module doesn't really have a concept of BuildConfig. This plugin allows us to create similar
    // looking BuildConfig constants in a java project.
    id 'com.github.gmazzo.buildconfig' version '3.0.0'
}

apply from: '../versioning/version_tasks.gradle'

project.ext.vstsUsername = System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") : project.findProperty("vstsUsername")
project.ext.vstsPassword = System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") : project.findProperty("vstsMavenAccessToken")

//labapi version.  Let's not change this please
version = getAppVersionName()

def functionApiClientCode = ""

if (project.hasProperty("functionApiCode")) {
    functionApiClientCode = functionApiCode
}

def resetApiClientCode = ""

if (project.hasProperty("resetApiCode")) {
    resetApiClientCode = resetApiCode
}

buildConfig {
    // forces the package. Defaults to '${project.group}'
    packageName("com.microsoft.identity.internal.test.labapi")
    useJavaOutput() // forces the outputType to 'java'
}

sourceSets {
    main {
        buildConfigField("String", "FUNCTION_API_CLIENT_CODE", "\"$functionApiClientCode\"")
        buildConfigField("String", "RESET_API_CLIENT_CODE", "\"$resetApiClientCode\"")
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.microsoft.identity'
            artifactId 'labapi'
            from components.java
        }
    }

    repositories {
        maven {
            name "vsts-maven-adal-android"
            url "https://identitydivision.pkgs.visualstudio.com/_packaging/AndroidADAL/maven/v1"
            credentials {
                username project.ext.vstsUsername
                password project.ext.vstsPassword
            }
        }
        maven {
            name "vsts-maven-android"
            url 'https://identitydivision.pkgs.visualstudio.com/IDDP/_packaging/Android/maven/v1'
            credentials {
                username project.vstsUsername
                password project.vstsPassword
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'io.swagger.core.v3:swagger-annotations:2.1.9'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.0'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okhttp:logging-interceptor:2.7.5'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'io.gsonfire:gson-fire:1.8.0'
    implementation 'org.threeten:threetenbp:1.3.5'
    testImplementation 'junit:junit:4.12'
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"
