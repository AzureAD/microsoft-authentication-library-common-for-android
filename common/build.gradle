plugins {
    id 'com.microsoft.identity.buildsystem' version '0.2.3'
    id 'com.android.library'
    id 'pmd'
    id 'checkstyle'
    id 'maven-publish'
    id 'kotlin-android'
}

apply from: '../versioning/version_tasks.gradle'

def desugarCode = false

if (project.hasProperty("sugar")) {
    desugarCode = sugar.toBoolean()
}

buildSystem {
    desugar = desugarCode
}

def enableCodeCoverage = false

if (project.hasProperty("codeCoverageEnabled")) {
    enableCodeCoverage = codeCoverageEnabled.toBoolean()
}

codeCoverageReport {
    coverage.enabled = enableCodeCoverage
}

// In dev, we want to keep the dependencies(common4j, broker4j, common) to 0.0.+ to be able to be consumed by daily dev pipeline.
// In release/*, we change these to specific versions being consumed.
def common4jVersion = "0.0.+"
if (project.hasProperty("distCommon4jVersion") && project.distCommon4jVersion != '') {
    common4jVersion = project.distCommon4jVersion
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        multiDexEnabled true
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode getAppVersionCode()
        versionName getAppVersionName()
        project.archivesBaseName = "common"
        project.version = android.defaultConfig.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //Languages Common supports.
        resConfigs "en", "ar", "b+sr+Latn", "bg", "ca", "cs", "da", "de", "el", "es", "et",
                "eu", "fi", "fr", "gl", "he", "hi", "hr", "hu", "in", "it", "iw", "ja",
                "kk", "ko", "lt", "lv", "ms", "nb", "nl", "pl", "pt-rBR", "pt-rPT", "ro",
                "ru", "sk", "sl", "sr", "sv", "th", "tr", "uk", "vi", "zh-rCN", "zh-rTW"
    }

    buildTypes {
        debug {
            testCoverageEnabled enableCodeCoverage
            minifyEnabled false
        }
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'consumer-rules.pro'
        }
    }
    //Upgrading to Java 8 in order to use YubiKit SDK
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError true
        disable 'MissingPermission'
        disable 'LongLogTag'
        disable 'DefaultLocale'
        disable 'UnusedResources'
        disable 'GradleDependency'
        disable 'GradleOverrides'
        disable 'OldTargetApi'
        disable 'RtlHardcoded'
    }

    testOptions {
        unitTests.all {
            exclude 'com/microsoft/identity/common/integration'
            exclude 'com/microsoft/identity/common/ropc'
        }
    }

    flavorDimensions "main"

    productFlavors {
        // The 'local' productFlavor sources common4j from mavenLocal and is intended to be used
        // during development.
        local {
            dimension "main"
        }

        // The 'dist' productFlavor sources common from a central repository and is intended
        // to be used for releases.
        dist {
            dimension "main"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'

            // Also load source from common-java
            java.srcDirs = ['src/main/java', 'src/main/kotlin']
            res.srcDirs = ['src/main/res']
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-${version}.aar"
        }
    }
}

dependencies {
    //Java 8 - Desugaring - Enabled/Disabled via plugin
    coreLibraryDesugaring libs.coreLibraryDesugaring

    localApi(project(":common4j")) {
        transitive = false
    }

    distApi("com.microsoft.identity:common4j:${common4jVersion}") {
        transitive = false
    }

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    implementation libs.displayMask
    implementation libs.httpclient
    implementation libs.nimbusJoseJwt
    implementation libs.appcompat
    implementation libs.gson
    implementation libs.browser
    implementation libs.constraintLayout
    implementation libs.kotlinStdlib
    implementation platform(libs.opentelemetryBom)
    implementation libs.opentelemetryApi
    implementation libs.fragment
    //For Executive Order work
    implementation libs.yubikitAndroid
    implementation libs.yubikitPiv

    // Test dependencies
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    // Needed to resolve JSONObject inside of non-instrumented tests
    testImplementation libs.json
    testImplementation libs.junit
    testImplementation libs.mockitoCore
    testImplementation libs.powermockModuleJunit4
    testImplementation libs.powermockModuleJunit4Rule
    testImplementation libs.powermockApiMockito2
    testImplementation libs.powermockClassloadingXstream
    testImplementation libs.robolectric
    testImplementation libs.androidxTestCore
    testImplementation libs.kotlinStdlib
    testImplementation project(":LabApiUtilities")

    // Instrumented dependencies
    androidTestImplementation libs.kotlinStdlib
    androidTestImplementation libs.androidxJUnit
    androidTestImplementation libs.mockitoAndroid
    androidTestImplementation(libs.espressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

// Create tasks to generate javadoc
android.libraryVariants.all { variant ->
    task("${variant.name}Javadoc", type: Javadoc, dependsOn: "assemble${variant.name.capitalize()}") {
        source = variant.javaCompileProvider.get().source

        title = "Microsoft Identity Android Common"

        // First add all of your dependencies to the classpath, then add the android jars
        doFirst { classpath += files(variant.javaCompiler.classpath.files) }
        classpath += files(android.getBootClasspath())

        options.memberLevel = JavadocMemberLevel.PUBLIC
        options.addStringOption('Xdoclint:none', '-quiet')

        // We're excluding these generated files
        exclude '**/R.java'
    }
}

// Create tasks to generate javadoc jar
android.libraryVariants.all { variant ->
    task("${variant.name}JavadocJar", type: Jar, dependsOn: "${variant.name}Javadoc") {
        classifier = 'javadoc'
        from tasks["${variant.name}Javadoc"].destinationDir
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
    destinationDirectory = reporting.file("$project.buildDir/outputs/jar/")
}

def configDir = new File(buildscript.sourceFile.parentFile.parentFile, 'config')
ext.checkstyleConfigDir = "$configDir/checkstyle"

checkstyle {
    ignoreFailures = false
    reportsDir = file("$project.buildDir/outputs/")
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")

    configProperties.checkstyleConfigDir = checkstyleConfigDir
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

pmd {
    ignoreFailures = false
    reportsDir = file("$project.buildDir/outputs/")
}

task pmd(type: Pmd) {
    description 'Run pmd'
    group 'verification'

    ruleSets = ["java-basic", "java-braces", "java-strings", "java-unusedcode"]
    source = fileTree('src/main/java')

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// For publishing to the remote maven repo.
afterEvaluate {

    publishing {
        publications {
            distRelease(MavenPublication) {
                from components.distRelease
                groupId 'com.microsoft.identity'
                artifactId 'common'
                //Edit the 'version' here for VSTS RC build
                version = project.version

                pom {
                    name = 'common'
                    description = 'This library contains code shared between the Active Directory ' +
                            'Authentication Library (ADAL) for Android and the Microsoft ' +
                            'Authentication Library (MSAL) for Android. This library ' +
                            'includes only internal classes and is NOT part of the ' +
                            'public API'
                    url = 'https://github.com/AzureAD/microsoft-authentication-library-common-for-android'
                    developers {
                        developer {
                            id = 'microsoft'
                            name = 'Microsoft'
                        }
                    }
                    licenses {
                        license {
                            name = 'MIT License'
                        }
                    }
                    inceptionYear = '2017'
                    scm {
                        url = 'https://github.com/AzureAD/microsoft-authentication-library-common-for-android/tree/master'
                    }
                    properties = [
                            branch : 'master',
                            version: project.version
                    ]
                }
            }
            distDebug(MavenPublication) {
                from components.distDebug
                groupId 'com.microsoft.identity'
                artifactId 'common-debug'
                //Edit the 'version' here for VSTS RC build
                version = project.version
            }
        }

        // Repositories to which Gradle can publish artifacts
        repositories {
            maven {
                name "vsts-maven-adal-android"
                url "https://identitydivision.pkgs.visualstudio.com/_packaging/AndroidADAL/maven/v1"
                credentials {
                    username System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") : project.findProperty("vstsUsername")
                    password System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") : project.findProperty("vstsMavenAccessToken")
                }
            }
            maven {
                name "vsts-maven-android"
                url 'https://identitydivision.pkgs.visualstudio.com/IDDP/_packaging/Android/maven/v1'
                credentials {
                    username System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_USERNAME") : project.findProperty("vstsUsername")
                    password System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDCOMMON_ACCESSTOKEN") : project.findProperty("vstsMavenAccessToken")
                }
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("assemble")) {
        task.dependsOn 'pmd'
    }

    if (task.name == 'assembleDebug' || task.name == 'assembleRelease') {
        task.dependsOn 'lint', 'sourcesJar', 'pmd'
    }
}
